apply plugin: 'idea'
apply plugin: 'project-report'
apply plugin: 'application'
apply plugin: 'versions'

buildscript {
  repositories {
    maven { url 'http://repo.obiba.org/obiba-gradle-plugins-release' }
  }
  dependencies {
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.1.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.5-beta-1'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.10'
}

ext {
  magmaVersion = '1.8-SNAPSHOT'
  slf4jVersion = '1.7.6'
}

mainClassName = 'org.obiba.magma.benchmark.MagmaBenchmark'

allprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: 'artifactory-publish'

  group = 'org.obiba.magma.benchmark'

  sourceCompatibility = JavaVersion.VERSION_1_7
  targetCompatibility = JavaVersion.VERSION_1_7

  defaultTasks 'build'

  repositories {
    mavenLocal()
    maven { url 'http://repo.obiba.org/repo' }
    mavenCentral()
  }

  tasks.withType(Compile) {
    options.encoding = 'UTF-8'
  }

  configurations {
    compile
    runtime
    all*.exclude group: 'commons-logging'
  }

  jar {
    manifest {
      attributes 'Implementation-Title': project.name,
          'Implementation-Version': project.version,
          'Implementation-Vendor-Id': project.group,
          'Implementation-Vendor': 'OBiBa'
    }
  }

  test {
    testLogging.showStandardStreams = true
  }

  // support incremental build for test task and artifactoryPublish
  // see http://www.practicalgradle.org/blog/2011/06/incremental-tests-with-jenkins/
  task jenkinsTest {
    inputs.files test.outputs.files
    doLast {
      def timestamp = System.currentTimeMillis()
      if (test.testResultsDir.exists()) {
        test.testResultsDir.eachFile { it.lastModified = timestamp }
      }
      if (test.testReportDir.exists()) {
        test.testReportDir.eachFile { it.lastModified = timestamp }
      }
    }
  }
  build.dependsOn(jenkinsTest)
  artifactoryPublish.dependsOn(jenkinsTest)

  // create jar with sources
  task packageSources(type: Jar, dependsOn: compileJava) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  // create jar with javadoc
  task packageJavadoc(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives packageSources
    archives packageJavadoc
  }

  // configure publication tu upload binary, POM.xml, sources & javadoc
  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        pom.withXml {
          def Node root = asNode();
          root.appendNode('name', 'magma-benchmark')
          root.appendNode('description', 'Benchmark for Magma abstraction layer')
          root.appendNode('url', 'http://www.obiba.org')

          def org = root.appendNode('organization')
          org.appendNode('name', 'OBiBa')
          org.appendNode('url', 'http://www.obiba.org')

          def issues = root.appendNode('issueManagement')
          issues.appendNode('system', 'jira')
          issues.appendNode('url', 'http://jira.obiba.org/jira/browse/MAGMA')

          def scm = root.appendNode('scm')
          scm.appendNode('url', 'https://github.com/obiba/magma-simple-benchmark')
          scm.appendNode('connection', 'scm:git:git://github.com/obiba/magma-simple-benchmark.git')
          scm.appendNode('developerConnection', 'scm:git:git://github.com/obiba/magma-simple-benchmark.git')

          def license = root.appendNode('licenses').appendNode('license');
          license.appendNode('name', 'GNU Public License version 3')
          license.appendNode('url', 'http://www.obiba.org/node/62')
          license.appendNode('distribution', 'repo')
        }
        artifact packageSources {
          classifier "sources"
        }
        artifact packageJavadoc {
          classifier "javadoc"
        }
      }
    }
  }

  // configure artifactory plugin
  artifactory {
    contextUrl = 'http://repo.obiba.org'
    publish {
      repository {
        // these settings are overridden by Artifactory Jenkins plugin
        repoKey = 'libs-snapshot-local'
        username = 'user'
        password = 'password'
      }
      defaults {
        publications('mavenJava')
      }
    }
    resolve {
      contextUrl = 'http://repo.obiba.org'
      repository {
        repoKey = 'libs-releases'
      }
    }
  }

}

rootProject.dependencies {
  compile project(':commons')
  compile project(':hibernate')
  compile project(':mongodb')
}

subprojects { subProject ->

  dependencies {
    // for snapshot, see http://java.dzone.com/articles/deploying-artifact-local-cache
    compile('com.google.code.findbugs:jsr305:2.0.3')
    compile('com.google.guava:guava:16.0.1')
    compile('net.sourceforge.findbugs:annotations:1.3.2')
    compile("org.obiba.magma:magma-api:${magmaVersion}")
    compile("org.obiba.magma:magma-xstream:${magmaVersion}")
    compile("org.obiba.magma:magma-data-generator:${magmaVersion}")
    compile("org.obiba.magma:magma-datasource-fs:${magmaVersion}")
    compile('org.obiba:obiba-core:1.5-SNAPSHOT')
    compile("org.slf4j:slf4j-api:${slf4jVersion}")
    compile('org.springframework:spring-context:3.2.5.RELEASE')
    compile('joda-time:joda-time:2.3')

    runtime('ch.qos.logback:logback-classic:1.1.1')
    runtime('javassist:javassist:3.12.1.GA')
    runtime("org.slf4j:slf4j-api:${slf4jVersion}")
    runtime("org.slf4j:jcl-over-slf4j:${slf4jVersion}")
  }

}
